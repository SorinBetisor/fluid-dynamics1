#version 450
layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 1) readonly buffer VorticityBuffer {
    float vorticity[];
};
layout(set = 0, binding = 3) readonly buffer StreamFunctionBuffer {
    float streamFuncScalars[];
};
layout(set = 0, binding = 4) writeonly buffer TempScalarBuffer {
    float tempScalar[];
};

layout(push_constant) uniform PushConstants {
    uvec2 gridDim; // Grid dimensions (width, height)
    float deltaTime; // Time step Δt
    float density; // Fluid density ρ (not directly used if viscosity is kinematic)
    float viscosity; // Kinematic viscosity ν
    int numPressureIterations; // Iterations for Poisson solver (max_it for ψ)
    int numOverallIterations; // Total simulation steps (unused in this shader)
    float omegaSOR; // Relaxation factor for SOR in Poisson solver (ω_SOR)
    float lidVelocity; // Velocity of the top lid (U0)
    float h; // Cell size (Δx = Δy = h)
} pc;

void main() {
    ivec2 ij = ivec2(gl_GlobalInvocationID.xy);
    uint W = pc.gridDim.x;
    uint H = pc.gridDim.y;
    uint idx = ij.y * W + ij.x;

    // Dirichlet ψ=0 on boundary
    if (ij.x == 0 || ij.y == 0 || ij.x >= int(W) - 1 || ij.y >= int(H) - 1) {
        tempScalar[idx] = 0.0;
        return;
    }

    uint ip = idx + 1;
    uint im = idx - 1;
    uint jp = idx + W;
    uint jm = idx - W;

    float psiC = streamFuncScalars[idx];
    float sumNbrs = streamFuncScalars[ip]
            + streamFuncScalars[im]
            + streamFuncScalars[jp]
            + streamFuncScalars[jm];
    float target = 0.25 * (sumNbrs + pc.h * pc.h * (-vorticity[idx]));
    // SOR update
    tempScalar[idx] = mix(psiC, target, pc.omegaSOR);
    // tempScalar[idx] = 3;
}
